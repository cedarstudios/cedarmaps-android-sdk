apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.cedarmaps.sdksampleapp"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 98080410
        versionName "1.6.0"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }

    flavorDimensions "dimension"

    productFlavors {
        def STRING = "String"
        def MARKET = "MARKET"

        def GOOGLE_PLAY = '"googleplay"'
        def CAFE_BAZAAR = '"cafebazaar"'

        googlePlay {
            buildConfigField STRING, MARKET, GOOGLE_PLAY
            dimension "dimension"
        }

        cafeBazaar {
            buildConfigField STRING, MARKET, CAFE_BAZAAR
            dimension "dimension"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
    }
}

ext.abiCodes = ['x86':1, 'x86_64':2, 'armeabi-v7a':3, 'arm64-v8a':4]
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = variant.versionCode + baseAbiVersionCode
        }
    }
}

dependencies {
    implementation (project(':CedarMapsSDK'))
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v7:0.6.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
}
